apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
        }
        javaCompileOptions.annotationProcessorOptions.arguments = [
                "room.incremental": "true",
                "room.schemaLocation": "$projectDir/schemas".toString(),
        ]
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions.jvmTarget = javaVersion

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

androidExtensions {
    experimental = true
}

cargo {
    module = 'src/main/rust/shadowsocks-rust'
    libname = 'sslocal'
    targets = ['arm', 'arm64', 'x86', 'x86_64']
    profile = findProperty('CARGO_PROFILE') ?: 'release'
    targetIncludes = ["lib${libname}.so"]
    extraCargoBuildArguments = ['--bin', 'sslocal']
    features {
        noDefaultBut "sodium", "rc4", "aes-cfb", "aes-ctr", "camellia-cfb", "openssl-vendored", "local-flow-stat", "local-dns-relay"
    }
    exec { spec, toolchain ->
        spec.environment("RUSTFLAGS", "-C lto=no -C link-arg=-o -C link-arg=target/${toolchain.target}/$profile/lib${libname}.so")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease') task.dependsOn 'cargoBuild'
}

def coroutinesVersion = '1.3.5'
def roomVersion = '2.2.5'
def workVersion = '2.3.4'
dependencies {
    api project(':plugin')
    api 'androidx.fragment:fragment-ktx:1.2.4'
    api "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    api "androidx.lifecycle:lifecycle-livedata-core-ktx:$lifecycleVersion"
    api 'androidx.preference:preference:1.1.1'
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.work:work-runtime-ktx:$workVersion"
    api "androidx.work:work-gcm:$workVersion"
    api 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    api 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    api 'com.google.code.gson:gson:2.8.6'
    api 'com.google.firebase:firebase-analytics:17.3.0'
    api 'com.google.firebase:firebase-config:19.1.3'
    api 'dnsjava:dnsjava:3.0.2'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutinesVersion"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarLibsVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidEspressoVersion"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.1'
}
